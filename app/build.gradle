apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion 29
  defaultConfig {
    applicationId 'com.blinkist.easylibrary'
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName '1.0'
    testInstrumentationRunner 'com.blinkist.easylibrary.TestInstrumentationRunner'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  sourceSets {
    String sharedTestDir = 'src/sharedTest/java'
    test {
      java.srcDir sharedTestDir
    }
    androidTest {
      java.srcDir sharedTestDir
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }
  dataBinding {
    enabled = true
  }
}

dependencies {
  // Kotlin and Android dependencies
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'androidx.appcompat:appcompat:1.2.0-alpha01'
  implementation 'androidx.activity:activity-ktx:1.1.0-rc03'
  implementation 'androidx.fragment:fragment-ktx:1.2.0-rc03'
  implementation 'com.google.android.material:material:1.2.0-alpha02'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

  // Architecture components
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc03'
  implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc03'
  implementation 'androidx.room:room-runtime:2.2.2'
  implementation 'androidx.room:room-ktx:2.2.2'
  kapt 'androidx.room:room-compiler:2.2.2'

  // Dagger
  implementation 'com.google.dagger:dagger:2.25.2'
  kapt 'com.google.dagger:dagger-compiler:2.25.2'

  // Coroutines
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

  // Retrofit
  implementation 'com.squareup.retrofit2:retrofit:2.6.2'
  implementation 'com.squareup.retrofit2:converter-moshi:2.6.2'
  implementation 'com.squareup.moshi:moshi:1.9.2'
  kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.2'

  // Glide
  implementation 'com.github.bumptech.glide:glide:4.10.0'

  // Timber
  implementation 'com.jakewharton.timber:timber:4.7.1'

  // RESTMock
  implementation('com.github.andrzejchm.RESTMock:android:0.4.1') {
    exclude group: 'androidx.test'
  }

  // Unit testing
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:3.1.0'
  testImplementation 'androidx.arch.core:core-testing:2.1.0'
  testImplementation 'com.google.truth:truth:1.0'

  // Instrumentation testing
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test:rules:1.2.0'
  androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
  androidTestImplementation 'com.google.truth:truth:1.0'
  kaptAndroidTest 'com.google.dagger:dagger-compiler:2.25.2'
}
