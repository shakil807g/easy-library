apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion buildConfig.compileSdk
  buildToolsVersion buildConfig.buildTools

  defaultConfig {
    applicationId 'com.blinkist.easylibrary'
    minSdkVersion buildConfig.minSdk
    targetSdkVersion buildConfig.targetSdk
    versionCode 1
    versionName '1.0'
    testInstrumentationRunner 'com.blinkist.easylibrary.TestInstrumentationRunner'
    testInstrumentationRunnerArguments clearPackageData: 'true'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [
            "room.schemaLocation": "$projectDir/schemas".toString(),
            "room.incremental"   : "true"
        ]
      }
    }
  }

  testOptions {
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    String sharedTestDir = 'src/sharedTest/java'
    test {
      java.srcDir sharedTestDir
    }
    androidTest {
      java.srcDir sharedTestDir
    }
  }

  dataBinding {
    enabled = true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
  }
}

dependencies {
  // kotlin
  implementation deps.kotlin.stdlib.jdk7
  implementation deps.kotlin.coroutines.core
  implementation deps.kotlin.coroutines.android
  testImplementation deps.kotlin.coroutines.test

  // android
  implementation deps.androidx.appCompat
  implementation deps.androidx.activityKtx
  implementation deps.androidx.fragmentKtx
  implementation deps.androidx.coreKtx
  implementation deps.androidx.preference

  // architecture components
  implementation deps.androidx.lifecycle.extensions
  implementation deps.androidx.lifecycle.ktx
  implementation deps.androidx.lifecycle.liveDataKtx
  implementation deps.androidx.lifecycle.viewModelKtx
  implementation deps.androidx.room.runtime
  implementation deps.androidx.room.ktx
  kapt deps.androidx.room.compiler

  // design
  implementation deps.material
  implementation deps.androidx.constraintLayout
  implementation deps.androidx.recyclerview
  implementation deps.androidx.swiperefreshlayout

  // dagger
  implementation deps.dagger.library
  kapt deps.dagger.compiler

  // http
  implementation deps.okhttp
  implementation deps.retrofit.library
  implementation deps.retrofit.moshiConverter
  implementation deps.moshi.library
  kapt deps.moshi.kotlinCodegen
  implementation(deps.restMock) { exclude group: 'androidx.test' }

  // coil
  implementation deps.coil

  // misc
  implementation deps.flowPreferences
  implementation deps.threeTenAbp
  implementation deps.timber

  // unit testing
  testImplementation deps.junit
  testImplementation deps.androidx.lifecycle.test
  testImplementation deps.mockito
  testImplementation deps.truth
  testImplementation deps.threeTenBp

  // instrumentation testing
  androidTestImplementation deps.androidx.test.core
  androidTestImplementation deps.androidx.test.runner
  androidTestImplementation deps.androidx.test.rules
  androidTestImplementation deps.androidx.test.junit
  androidTestImplementation deps.androidx.test.espresso.core
  androidTestImplementation deps.androidx.test.espresso.contrib
  androidTestImplementation deps.truth
  androidTestUtil deps.androidx.test.orchestrator
  kaptAndroidTest deps.dagger.compiler
}
